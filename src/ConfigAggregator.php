<?hh // strict

namespace Ytake\HHConfigAggreagator;

class ConfigAggreagator {
  const string ENABLE_CACHE = 'config_cache_enabled';
  private array<mixed, mixed> $config = [];

  public function __construct(
    array<ConfigProvidable> $providers = [],
    ?string $cachedConfigFile = null,
  ) {
    if ($this->loadConfigFromCache($cachedConfigFile)) {
      return;
    }
    $this->config = $this->loadConfigFromProviders($providers)->toArray();
    $this->cacheConfig($this->config, $cachedConfigFile);
  }

  private function loadConfigFromCache(?string $cachedConfigFile): bool {
    if (null === $cachedConfigFile) {
      return false;
    }
    $require = new Filesystem($cachedConfigFile);
    if (!$require->exists()) {
      return false;
    }
    $this->config = $require->require();
    return true;
  }

  private function loadConfigFromProviders(
    array<ConfigProvidable> $providers,
  ): Map<mixed, mixed> {
    $mergedConfig = [];
    $configArray = [];
    foreach ($providers as $provider) {
      $configArray[] = $provider->provide();
    }
    return $this->mergeArrayToMap($configArray);
  }

  private function mergeArrayToMap(
    array<mixed, mixed> $a
  ): Map<mixed, mixed> {
    $map = Map {};
    foreach ($a as $v) {
      if (is_array($v)) {
        foreach ($v as $key => $row) {
          if (is_int($key) || is_string($key)) {
            $map->add(Pair {$key, $row});
          }
        }
      }
    }
    return $map;
  }

  /**
   * @return array<mixed, mixed>
   */
  public function getMergedConfig(): array<mixed, mixed> {
    return $this->config;
  }

  protected function cacheConfig(
    array<mixed, mixed> $configMap,
    ?string $cachedConfigFile = null,
  ): void {
    if (null === $cachedConfigFile) {
      return;
    }
    if (!array_key_exists(static::ENABLE_CACHE, $configMap)) {
      return;
    }
    if (!$configMap[static::ENABLE_CACHE]) {
      return;
    }
    file_put_contents(
      $cachedConfigFile,
      sprintf(
        '<?hh
        /**
        * This configuration cache file was generated by %s
        * at %s
        */
        return %s;',
        get_class($this),
        date('c'),
        var_export($configMap, true),
      ),
    );
    return;
  }
}
